apply plugin: 'com.android.application'
apply from: "${project.rootDir}/buildsystem/android-defaults.gradle"

android {

    final int buildNumber = getBuildNumber()
    final int versionMajor = 1
    final int versionMinor = 0
    final int versionPatch = buildNumber
    final int androidVersionCode = buildNumber

    final String semanticVersion = "${versionMajor}.${versionMinor}.${versionPatch}"

    signingConfigs {
        release {
            storeFile file(System.getenv("ANDROID_KEYSTORE") ?: "[KEY_NOT_DEFINED]")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }

        debug {
            storeFile file("${project.rootDir}/buildsystem/signing_keys/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    defaultConfig {
        versionCode androidVersionCode
        versionName semanticVersion

        buildConfigField "boolean", "STRICT_MODE", "false"
    }

    buildTypes {
        release {
            minifyEnabled false
            resValue "string", "app_name", "KeyEvent Display"
            if(isRunningOnCi()) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug

            resValue "string", "app_name", "Debug KeyEvent Display"

            buildConfigField "boolean", "STRICT_MODE", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation project(':monitors')

    implementation "com.karumi:dexter:$dexter_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.annotation:annotation:$androidx_annotation_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
}